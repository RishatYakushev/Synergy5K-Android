apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: "de.mannodermaus.android-junit5"
apply from: './scripts/versioning.gradle'

detekt {
    toolVersion = "$versions.detekt"
    input = files("src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("../default-detekt-config.yml")
}

android {
    compileSdkVersion versions.TARGET_SDK_VERSION

    defaultConfig {
        applicationId "com.synergy.android"
        minSdkVersion versions.MIN_SDK_VERSION
        targetSdkVersion versions.TARGET_SDK_VERSION
        versionCode appVersionCode.toInteger()
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder",
                "de.mannodermaus.junit5.AndroidJUnit5Builder"
        buildConfigField 'String', 'BASE_URL', "\"https://api.com\""
    }
    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.jks")
            storePassword "12345678"
            keyAlias "debug"
            keyPassword "12345678"
        }
        release {}
    }
    buildTypes {
        release {
            ext.betaDistributionGroupAliases = "synergy"
            ext.betaDistributionNotifications = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }
    lintOptions {
        warningsAsErrors true
        htmlOutput file("build/reports/lint/lint-report.html")
        xmlOutput file("build/reports/lint/lint-report.xml")
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    androidExtensions {
        experimental = true
    }
}

repositories {
    maven { url = uri("https://dl.bintray.com/kodein-framework/Kodein-DI") }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"

    implementation androidLibs
    implementation firebaseLibs
    implementation retrofitLibs
    implementation coroutinesLibs
    implementation kodeinLibs
    implementation roomLibs
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt compilerLibs

    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs

    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"
    implementation 'com.google.android.gms:play-services-location:17.0.0'
}

apply plugin: 'com.google.gms.google-services'
